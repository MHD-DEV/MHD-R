SUBROUTINE FOBJ
!ESTA SUBROTINA ANALISA A QUALIDADE DO AJUSTE ENTRE OS HIDROGRAMAS CALCULADOS E OBSERVADOS
!COM BASE EM ALGUMAS FUN��ES OBJETIVO
USE VARS_MAIN
IMPLICIT NONE
INTEGER IB
! VARIAVEIS AUXILIARES
REAL SOMARES,SOMALRES,SOMAX2,SOMAY2,SOMAXY,SOMALX2,SOMALY2,SOMALXY,XX,YY,XY 
REAL SOMAOBS !SOMATORIO DOS VALORES DE VAZ�O OBSERVADOS
REAL SOMACAL !SOMATORIO DOS VALORES DE VAZ�O CALCULADOS
REAL SOMALOBS !SOMATORIO DOS LOGARITMOS DE VAZ�O OBSERVADOS
REAL SOMALCAL !SOMATORIO DOS LOGARITMOS DE VAZ�O CALCULADOS
INTEGER ITVAL !CONTA O NUMERO DE DIAS EM QUE EXISTE VAZ�O OBSERVADA PARA COMPARAR
REAL XMOBS,XMLOBS !VAZ�O M�DIA OBSERVADA E VAZAO MEDIA DOS LOGARITMOS DAS OBSERVACOES
REAL XMCAL,XMLCAL !VAZ�O M�DIA CALCULADA E VAZAO MEDIA DOS LOGARITMOS DAS VAZOES CALCULADAS
!REAL NASH(NB),LNASH(NB),R2(NB),ERRV(NB),ERRE(NB),R2L(NB) !COEF. NASH, ERRO NOS VOLUMES, COEF NASH LOGARITMOS

!QR(NOBS,NT),QOBS(NOBS,NT) !VAZ�O NOS EXUTORIOS DAS BACIAS: CALCULADA, OBSERVADA
! NOBS,IQOBS(NOBS) !NUMERO DE SERIES DE VAZAO OBSERVADA

NASH=0.0
R2=0.0
R2L=0.0
ERRV=99.0
ERRE=99.0

DO IB=1,NOBS
    IF(ICALIB == 1 .AND. IB /= NBVFO) CYCLE ! SE ESTIVER CALIBRANDO, A FUNCAO OBJETIVA CALCULADA EH SO DO POSTO 
	SOMAOBS=0.0
	SOMACAL=0.0
	SOMALOBS=0.0
	SOMALCAL=0.0
	ITVAL=0
	SOMAX2=0.
	SOMAY2=0.
	SOMAXY=0.
	SOMALX2=0.
	SOMALY2=0.
	SOMALXY=0.
	SOMARES=0.
	SOMALRES=0.
    	
	IF(INIAJU <= NT)THEN
		DO IT=INIAJU,NT
			IF(QOBS(IB,IT) >= 0.0) THEN
				SOMAOBS=SOMAOBS+QOBS(IB,IT)
				SOMACAL=SOMACAL+QR(IB,IT)
				XX=QOBS(IB,IT)-QR(IB,IT)
				SOMARES= SOMARES + XX*XX ! SOMA RESIDUOS
				!LOGARITMOS
				XX=LOG(QOBS(IB,IT)+0.0000001)
				YY=LOG(QR(IB,IT)+0.0000001)
				SOMALOBS=SOMALOBS+XX !SOMA UM VALOR MUITO PEQUENO PARA EVITAR LOGARITMO DE ZERO
				SOMALCAL=SOMALCAL+YY !SOMA UM VALOR MUITO PEQUENO PARA EVITAR LOGARITMO DE ZERO
				SOMALRES=SOMALRES+(XX-YY)*(XX-YY) ! SOMA RESIDUOS DOS LOGARITMOS
!				SOMAPREB=SOMAPREB+PREB(IB,IT) !SOMA PRECIPITACAO DA BACIA
!				SOMAECAL=SOMAECAL+ETTB(IB,IT) ! SOMA EVAPORACAO TOTAL CALCULADA DA BACIA
				ITVAL=ITVAL+1
			ENDIF
		ENDDO
        IF(ITVAL > 25) THEN
			XMOBS=SOMAOBS/ITVAL ! MEDIA DAS VAZOES OBSERVACOES
			XMLOBS=SOMALOBS/ITVAL ! MEDIA DO LOGARITMO DAS OBSERVACOES
			XMCAL=SOMACAL/ITVAL ! MEDIA DAS VAZOES CALCULADAS
			XMLCAL=SOMALCAL/ITVAL ! MEDIA DOS LOGARITMOS DAS VAZOES CALCULADAS
			DO IT=INIAJU,NT
				IF(QOBS(IB,IT) >= 0.0) THEN
				    YY=QR(IB,IT)-XMCAL
				    XX=QOBS(IB,IT)-XMOBS
					SOMAXY=SOMAXY + XX*YY
				    SOMAX2= SOMAX2 + XX*XX
				    SOMAY2= SOMAY2 + YY*YY
				    YY=LOG(QR(IB,IT)+0.0000001)-XMLCAL
				    XX=LOG(QOBS(IB,IT)+0.0000001)-XMLOBS
					SOMALX2= SOMALX2 + XX*XX
				    SOMALY2= SOMALY2 + YY*YY
				    SOMALXY= SOMALXY + XX*YY
				ENDIF					
			ENDDO
			
			!COEFICIENTE DE NASH
			NASH(IB)=1.-SOMARES/SOMAX2
			!COEFICIENTE DE NASH DOS LOGARTIMOS DAS VAZOES
			LNASH(IB)=1.-SOMALRES/SOMALX2
			!ERRO DE VOLUME
			ERRV(IB)=(SOMACAL-SOMAOBS)/SOMAOBS
			!COEFICIENTE DE CORRELACAO: R2
			R2(IB)=SOMAXY*SOMAXY/(SOMAX2*SOMAY2)
			!COEFICIENTE DE CORRELACAO DOS LOGARITMOS: R2LOG
			R2L(IB)=SOMALXY*SOMALXY/(SOMALX2*SOMALY2)
		
		ENDIF
	ENDIF
ENDDO	

IF(ICALIB == 1) THEN ! ESTA CALIBRANDO, CALCULA O VALOR DA FUNCAO OBJETIVO
    !FOB = 10000*( (1-NASH(NBVFO)))
    !FOB = 0.33*SQDES+0.33*SQLOG+0.33*(SOMACAL/SOMAOBS-1.0)*100.
    !FOB=SUMRES
    
    FOB = 10000*( (1-NASH(NBVFO)) + (1-LNASH(NBVFO)))
    
    !FOB = 10000*( (1-R2(NBVFO)) + (1-R2L(NBVFO)) )
    !IF(FOB<0) THEN
    !WRITE(*,*) FOB,NASH(NBVFO),R2(NBVFO),R2L(NBVFO)
    !PAUSE
    !ENDIF
    
    RETURN
ELSE
    WRITE(*,*)	
    WRITE(*,*)'ESTATISTICAS:'
    OPEN(FILAJU,FILE=DIR_DADOS//'Saida/ESTATISTICAS.HIG',STATUS='UNKNOWN')
    WRITE(*,'(A8,8A8)')'BACIA','NASH','LNASH','R2','R2L','ERRV','ERRE','ETCAL','ETOBS'
    WRITE(FILAJU,'(A8,8A8)')'BACIA','NASH','LNASH','R2','R2L','ERRV','ERRE','ETCAL','ETOBS'
    DO IB=1,NOBS 
        WRITE(*,'(I8,8F8.3)')IB,NASH(IB),LNASH(IB),R2(IB),R2L(IB)
        WRITE(FILAJU,'(I8,8F8.3)')IB,NASH(IB),LNASH(IB),R2(IB),R2L(IB)
    ENDDO
    WRITE(*,*)
    CLOSE(FILAJU)
ENDIF

RETURN
END
