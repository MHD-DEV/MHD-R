SUBROUTINE TRANSPIRACAO(IC,IU,IB)
USE VARS_MAIN
IMPLICIT NONE
INTEGER IC,IU,IB
REAL RCX,ETSSU,ETRAD,ETSUB,FSSS,FRAD,FSUB,OMEGA,A,FR1,FR2,FR3,SCRIT,SCRIT2,PSICR,SECR,AUX
REAL ETBS
!
!   TRANSPIRACAO
!   CALCULADA USANDO O MODELO DE JARVIS (1989) A SIMPLE EMPIRICAL MODEL OF ROOT WATER UPTAKE
!
!VARIABLES DE ENTRADA
! PRAD		: PROFUNDIDADE DAS RAIZES
! E0		: EVAPORACAO DE UMA SUPERFICIE DE AGUA LIVRE
! SSSU		: ARMAZENAMENTO NA CAMADA SUPERIOR DO SOLO
! SRAD		: ARMAZENAMENTO NA SEGUNDA CAMADA DO SOLO
! SSUB		: ARMAZENAMENTO NA CAMADA INFERIOR DO SOLO
! SCSS		: PORCENTAGEM DE SSMAX QUE INICIA ESTRESSE HIDRICO NA CAMADA SUPERIOR DO SOLO
! SCSUB		: PORCENTAGEM DE SMAX QUE INICIA ESTRESSE HIDRICOS NA CAMADA INFERIOR DO SOLO
! RC		: RESISTENCIA MINIMA DO DOSSEL
! RSBS		: RESISTENCIA DO SOLO NU - PARAMETRO
! D1		: ESPESSURA DA CAMADA SUPERIOR DO SOLO
! FR        : FATOR DE DISTRIBUICAO DE RAIZES

!VARIABLES DE SAIDA
! ETT	: EVAPOTRANSPIRACAO TOTAL
! SSSU	: ARMAZENAMENTO NA CAMADA SUPERIOR DO SOLO ATUALIZADO	 
! SSUB	: ARMAZENAMENTO NA CAMADA INFERIOR DO SOLO ATUALIZADO
! ETP	: TRANSPIRACAO POTENCIAL


!VARIABLES INTERNAS 

! ETSSU : EVAPORACAO DA CAMADA SUPERIOR DO SOLO
! ETSUB	: EVAPORACAO DA CAMADA INFERIOR DO SOLO
! RCX	: RESISTENCIA SUPERFICIAL
! ETR	: TRANSPIRACAO REAL
! A		: ENERGIA DISPONÍVEL PARA TRANSPIRACAO
! OMEGA	: INDICE DE ESTRESSE PONDERADO
! OMEGAC: VALOR CRÍTICO DO INDICE DE ESTRESSE PONDERADO
! FRAIZES: PORCENTAGEM DE RAICES NA CAMADA SUPERIOR
! 

! CALCULA A QUANTIDADE DE ENERGIA DISPONIVEL PARA EVAPORACAO DESCONTANDO A INTERCEPTACAO
IF(EC(IC,IU) < 0.) THEN
    ! SE FOR ORVALHO, JÁ FOI CONSIDERADO NO CALCULO DE ESCOAMENTO
    EC(IC,IU)=0.
ENDIF
ETT(IC,IU) = EC(IC,IU) ! EVAPORACAO TOTAL
IF(ASAT(IC,IU) >= 1.) THEN ! TODA A CELULA ESTA SATURADA
	ETP(IC,IU)=0.0 ! NAO HA TRANSPIRACAO
    A = E0AG-EC(IC,IU) ! ENERGIA DISPONIVEL
	IF(A > 0.) THEN
	    ETT(IC,IU) = ETT(IC,IU) + A 	! ATUALIZA A EVAPORACAO TOTAL
        ! ATUALIZA ARMAZENAMENTO DA TERCEIRA CAMADA DE SOLO, A EVAPORACAO DA AREA SATURADA SAI DESSA CAMADA
        SSUB(IC,IU) = SSUB(IC,IU) - A
        IF(SSUB(IC,IU) < 0.) THEN
            ETT(IC,IU)  = ETT(IC,IU) + SSUB(IC,IU) 
	        SSUB(IC,IU) = 0.
        ENDIF
    ENDIF
ELSE ! CELULA COM FRACAO DA AREA NAO COBERTA PELA AGUA
	!EVAPORACAO DA AREA SATURADA DO BLOCO EH CALCULADA COMO EVAPORACAO DA CAMADA 3 POIS ELA CONTROLA A AREA SATURADA
	!TERCEIRA CAMADA: EVAPORACAO DA AREA SATURADA + TRANSPIRACAO
	ETSUB = ASAT(IC,IU)*MAX(E0AG-EC(IC,IU),0.)
	
	! TRANSPIRACAO E EXTRACAO RADICULAR
    A = E0-EC(IC,IU) ! ENERGIA DISPONIVEL
    IF(A > 0.) THEN ! EXISTE ENERGIA DISPONIVEL PARA TRANSPIRACAO
	    ! EVAPORACAO DO SOLO NU
 	    ETBS=A*(DELTA+GAMMA)/(DELTA+GAMMA*(1.+52./RA)) ! EVAPORACAO POTENCIAL SOLO DESNUDO
 	    IF(D1(IB) < 0.1) THEN ! CAMADA MENOR QUE 10 CM
 	        AUX=1.
 	    ELSE
 	        AUX=1.-0.3/D1(IB) ! ACOCHAMBRADO, VISANDO REDUZIR A UMIDADE A SUPERFICIE QUANDO A CAMADA 1 EH ESPESSA
 	    ENDIF
 	    ! CONSIDERO QUE O ARMAZENAMENTO DA CROSTA DE SOLO É PROPORCIONAL A ESPESSURA
 	    SCRIT=AUX*SSSU(IC,IU)/(SSMAX(IB,IU)*SECRBS(IU)) 
	    IF(SCRIT < 1.) THEN
            ETBS=ETBS*0.25*(1-COS(PI*SCRIT))**2. ! LEE AND PIELKE, 1991
	    ENDIF
		
	    RCX=RC(IU)/IAF(IU,IMES) !RESISTENCIA SUPERFICIAL DA VEGETACAO
	    ETP(IC,IU)=A*(DELTA+GAMMA)/(DELTA+GAMMA*(1.+RCX/RA)) ! TRANSPIRACAO POTENCIAL
       
       ! DEFINE FATORES DE ESTRESSE DO MODELO DE JARVIS
       ! ETP = 5 MM/DIA PSI_C= -20 KPA
       ! ETP = 1 MM/DIA PSI_C= -80 KPA
        SECR=MIN(1.,SECRJAH(IU)*ETP(IC,IU)+SECRJAL(IU))
        SECR=MAX(SECR,0.)
   	    !FATOR DE ESTRESSE DEVIDO A UMIDADE DE SOLO NA CAMADA SUPERIOR DO SOLO
        SCRIT=SECR*SSMAX(IB,IU)
        FSSS=MIN(SSSU(IC,IU)/SCRIT,1.)
        
	    IF(PRAD(IU,IMES) < D1(IB)) THEN
	        ! A PROFUNDIDADE RADICULAR E MENOR QUE A ESPESSURA DA CAMADA SUPERIOR:
            ! NAO HA EVAPORACAO NA CAMADAS INFERIORES
	        FR1 = 1.0
	        FR2 = 0.0
	        FRAD = 0.0
	        FR3 = 0.0
	        FSUB = 0.0
        ELSE
            !FATOR DE ESTRESSE DEVIDO A UMIDADE DE SOLO DA SEGUNDA CAMADA
            SCRIT=MAX(0.001,SECR*SRMAX(IB,IU)) ! ARMAZENAMENTO CRITICO DA SEGUNDA CAMADA
    	    FRAD=MIN(SRAD(IC,IU)/SCRIT,1.)
	        FR1 = 1.0 - EXP(-FR * D1(IB) / PRAD(IU,IMES)) 
            IF (PRAD(IU,IMES) < D1(IB)+D2(IB)) THEN 
	            ! SE A PROF RADICULAR FOR MENOR QUE D1+D2, O ARMAZENAMENTO DISPONIVEL PARA
	            ! EVAPORACAO PROFUNDA EH DADO PELA SEGUNDA CAMADA
	            FR2 = 1.0 - FR1
	            FR3 = 0. 
	            FSUB= 0.
	        ELSE
                !FATOR DE ESTRESSE DEVIDO A UMIDADE DE SOLO NA CAMADA INFERIOR DO SOLO
                SCRIT=SECR*SMAX(IB,IU) ! ARMAZENAMENTO CRITICO DA CAMADA INFERIOR
	            ! SE PRAD >D1+D2, SRAD > 0 E A AGUA DISPONIVEL PARA TRANSPIRACAO 
	            ! PROFUNDA SAI TAMBEM DA TERCEIRA CAMADA
    	        FSUB=MIN(SSUB(IC,IU)/SCRIT,1.)
    	        
		        FR2 = 1.0 - FR1 - EXP(-FR * (D1(IB)+D2(IB)) / PRAD(IU,IMES))
    	        FR3 = 1.0 - FR1 - FR2
	        ENDIF
        ENDIF
	    ! FATOR DE COMPENSACAO PONDERADO
	    OMEGA=FR1*FSSS+FR2*FRAD+FR3*FSUB
		
	    !OMEGA=MAX(OMEGA/OMEGAC,1.)-1.
	    !AUX=MAX(0.,FSSS/OMEGAC-OMEGA)
	    OMEGA=MAX(OMEGA,OMEGAC)
		
	    !CAMADA SUPERIOR DE SOLO: 
	    !EVAPORACAO SOLO DESNUDO + TRANSPIRACAO NA AREA NAO SATURADA
	    ETSSU = (1.- ASAT(IC,IU))*(COVER(IU,IMES)*ETP(IC,IU)*FR1*FSSS/OMEGA + (1.-COVER(IU,IMES))*ETBS)
        
        !SEGUNDA CAMADA: TRANSPIRACAO NA AREA NAO SATURADA
		ETRAD = (1.- ASAT(IC,IU))*COVER(IU,IMES)*FR2*ETP(IC,IU)*FRAD/OMEGA

	    !EVAPORACAO DA AREA SATURADA DO BLOCO EH CALCULADA COMO EVAPORACAO
	    !DA CAMADA 3 POIS ELA CONTROLA A AREA SATURADA
	    !TERCEIRA CAMADA: EVAPORACAO DA AREA SATURADA + TRANSPIRACAO
	    ETSUB = ETSUB + (1.- ASAT(IC,IU))*COVER(IU,IMES)*FR3*ETP(IC,IU)*FSUB/OMEGA
    ELSE ! NA EXISTE ENERGIA DISPONIVEL PARA TRANSPIRACAO
        ETSSU=0.
        ETRAD=0.        
    ENDIF
    		
    ! ATUALIZA OS ARMAZENAMENTOS

    ! CAMADA SUPERIOR DO SOLO
    SSSU(IC,IU) = SSSU(IC,IU) - ETSSU
    IF(SSSU(IC,IU) < 0.) THEN
	    ETSSU = ETSSU + SSSU(IC,IU)
	    SSSU(IC,IU) = 0.
    ENDIF
    
    ! SEGUNDA CAMADA DE SOLO
    SRAD(IC,IU) = SRAD(IC,IU) - ETRAD
    IF(SRAD(IC,IU) < 0.) THEN
	    ETRAD = ETRAD + SRAD(IC,IU)
	    SRAD(IC,IU) = 0.
    ENDIF

    ! TERCEIRA CAMADA DE SOLO
    SSUB(IC,IU) = SSUB(IC,IU) - ETSUB
    IF(SSUB(IC,IU) < 0.) THEN
        ETSUB  = ETSUB + SSUB(IC,IU) 
	    SSUB(IC,IU) = 0.
    ENDIF
    
    ! EVAPORACAO TOTAL
    ETT(IC,IU) = ETT(IC,IU) + ETSSU + ETRAD + ETSUB
		
ENDIF

RETURN
END
	