SUBROUTINE LESOLO

!esta subrotina le o arquivo dos parametros de 12 TIPOS DE solo	DO TRIANGULO TEXTURAL (aRquivo PARSOLO.HIG)

!estes parametros sao multiplciados pelos coeficientes definidos no arquivo AJUSTE.HIG.


USE VARS_MAIN
USE VARS_CALIB ! ALOCA VALORES DE REFERENCIA DOS PARAMETROS DE SOLO PARA USAR NA CALIBRACAO
IMPLICIT NONE
INTEGER IB,IS,IU
REAL DKSS,DTSUB,DMU,DALPHA,DCSI,DCS,DCB 	!PARAMETROS USADOS NO AJUSTE
REAL PSICRH,PSICRL,BBC

SMAX=0.0
SSMAX=0.
SRMAX=0.
KSS=0.0
TSUB=0.0
MU=1.0
NETA=1.0
CS=0.0
CB=0.0
CSI=0.

OPEN(FILSOLO,FILE=DIR_DADOS//'Entrada/ParSolo.hig',STATUS='OLD')	
! LE OS PARAMETROS DE BROOKS COREY DE 13 SOLOS DO TRAINGULO TEXTURAL DO USDA
! KSAT CONDUTIVIDADE HIDRAULICA SATURADA (MM/H)
! B PARAMETRO DA CURVA DE RETENCAO
! PSIB PRESSAO DE ENTRADA DO AR (KPA)
! THS UMIDADE VOLUMETRICA NA SATURACAO
! THR UMIDADE VOLUMETRICA RESIDUAL
READ(FILSOLO,72) TITULO
READ(FILSOLO,72) TITULO
READ(FILSOLO,71) (TITULO,KSAT(IS),PSI(IS),B(IS),THS(IS),THR(IS),IS=1,13)
CLOSE(FILSOLO)
ALLOCATE (CSI_REF(NU))

DO IU=1,NU
    IF(SOLO(IU) /= 14) THEN ! SOLOS 14 = AGUA
       ! POTENCIAL DE ENTRADA DO AR E B DE BROOKS COREY
        BBC=1./B(SOLO(IU))
        ! DEFINE CSI = CAPACIDADE DE CAMPO = 10 KPA
        CSI_REF(IU)=THR(SOLO(IU))+(THS(SOLO(IU))-THR(SOLO(IU)))*(MIN(PSI(SOLO(IU))/10.,1.))**BBC       
        CSI_REF(IU)=(CSI_REF(IU)-THR(SOLO(IU)))/(THS(SOLO(IU))-THR(SOLO(IU)))
        ! LIMITE DE AGUA DISPONIVEL PARA EVAPORACAO DE SOLO NU
	    SECRBS(IU)=CSI_REF(IU)
        ! DEFINE FATORES DE ESTRESSE DO MODELO DE JARVIS
        ! ETP = 5 MM/DIA PSI_C= -20 KPA
        ! ETP = 1 MM/DIA PSI_C= -80 KPA
        PSICRH=20
        PSICRL=80
        SECRJAH(IU)=(MIN(PSI(SOLO(IU))/PSICRH,1.))**BBC ! SATURACAO EFETIVA CRITICA DE INICIO DE ESTRESSE PARA ETP=5MM/DIA
        SECRJAL(IU)=(PSI(SOLO(IU))/PSICRL)**BBC ! SATURACAO EFETIVA CRITICA DE INICIO DE ESTRESSE PARA ETP=1MM/DIA
        ! TRANSFORMA OS VALORES PARA CALCULO LINEAR
        SECRJAH(IU)=(SECRJAH(IU)-SECRJAL(IU))/((5.0-1.0)*DTP/86400)
        SECRJAL(IU)=SECRJAL(IU)-SECRJAH(IU)*1.*DTP/86400
    ENDIF
ENDDO

OPEN(FILBACIA,FILE=DIR_DADOS//'Entrada/ParBacia.hig',STATUS='OLD')
! LE PARAMETROS DE ESCOAMENTO IN-CELL DE CADA SUB-BACIA
READ(FILBACIA,72) TITULO
READ(FILBACIA,*) TSUB_REF
READ(FILBACIA,*) MU_REF
READ(FILBACIA,*) ALPHA_REF
READ(FILBACIA,*) CS_REF
READ(FILBACIA,*) BC1,BC2,BC3
READ(FILBACIA,*) QMESP
READ(FILBACIA,72) TITULO
READ(FILBACIA,73) (TITULO,CB(IB),QESP(IB),QCONS(IB),IB=1,NB)
CLOSE (FILBACIA)

! LE FATORES DE PONDERACAO DE CADA SUB-BACIA
OPEN(FILAJUSTE,FILE=DIR_DADOS//'Entrada/ParAjuste.hig',STATUS='OLD')
READ(FILAJUSTE,*) 
DO IB=1,NB
    ! D2	:ESPESSURA DA SEGUNDA CAMADA DO SOLO
    ! D3    :ESPESSURA DA TERCEIRA CAMADA DO SOLO
    ! D1	:ESPESSURA DA CAMADA SUPERIOR DO SOLO
    ! DKSS	:FATOR DE PONDERACAO DO PARAMETRO KSS 
    ! DTSUB	:FATOR DE PONDERACAO DO PARAMETRO TSUB
    ! DMU	:FATOR DE PONDERACAO DO PARAMETRO TSUB
 	! cuidado aqui, verificar sempre a ordem de leitura dos paramentros    	
 	READ(FILAJUSTE,'(A10,10G10.0)') TITULO,D1(IB),D2(IB),D3(IB),DKSS,DTSUB,DMU,DALPHA,DCSI,DCS,DCB
    DO IU=1,NU
        IF(SOLO(IU) /= 14) THEN
            ! CALCULA OS PARAMETROS DE CADA SUB-BACIA
            SSMAX(IB,IU)=(THS(SOLO(IU))-THR(SOLO(IU)))*1000*D1(IB) ! ARMAZENAMENTO MAXIMO CAMADA SUPERIOR DO SOLO
	        SRMAX(IB,IU)=(THS(SOLO(IU))-THR(SOLO(IU)))*1000*D2(IB) ! ARMAZENAMENTO MAXIMO EM MM NA SEGUNDA CAMADA
            SMAX(IB,IU)=(THS(SOLO(IU))-THR(SOLO(IU)))*1000*D3(IB) ! ARMAZENAMENTO MAXIMO EM MM NA TERCEIRA CAMADA
            ALPHA(IB,IU)=ALPHA_REF*DALPHA ! COEFICIENTE DE ANISOTROPIA
            KSS(IB,IU)=(KSAT(SOLO(IU))*24./1000.)*DKSS ! CONDUTIVIDADE HIDRAULICA SATURADA CAMADA SUPERIOR M/DIA
            NETA(IU)=2.5+2.*B(SOLO(IU)) ! COEFICIENTE DA  CONDUTIVDADE HIDRAULICA NAO SATURADA
            TSUB(IB,IU)=TSUB_REF*DTSUB ! TRANSMISSIVIDADE MAXIMA
            MU(IB,IU)=MU_REF*DMU ! 
	        CSI(IB,IU)=MIN(CSI_REF(IU)*DCSI,1.) ! CAPACIDADE DE CAMPO
        ENDIF
    ENDDO
   ! pause
	CS(IB)=CS_REF*DCS
    CB(IB)=CB(IB)*DCB*86400. ! CB CALCULADO EM DIAS E TRANSFORMADO A SEGUNDOS
ENDDO
!    ibcal=1
!    do iu=1,nu
!       WRITE(9999,'(2i10,10f10.3)') ibcal,iu,SSMAX(IBCAL,iu),SRMAX(IBCAL,iu),SMAX(IBCAL,iu),ALPHA(IBCAL,iu),KSS(IBCAL,iu),TSUB(IBCAL,iu),MU(IBCAL,iu),CSI(IBCAL,iu),CS(IBCAL),CB(IBCAL)
!    enddo

CLOSE (FILAJUSTE)
71	FORMAT(A15,5G10.0)
72	FORMAT(A70)
73	FORMAT(A10,3G10.0)
RETURN
END
	
